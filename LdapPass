#!/usr/bin/env perl
use Mojolicious::Lite;
use Net::LDAP;
use Crypt::SmbHash qw(lmhash nthash);

use lib './';
use PasswordTools;

#use Data::Dumper;

# Documentation browser under "/perldoc" (this plugin requires Perl 5.10)
#plugin 'pod_renderer';

any '/index' => sub {
    my $self = shift;
    my $config = plugin 'JSONConfig' => { file => 'LdapPass.json', };

    my $method = $self->req->method;

    if ( $method eq 'POST' ) {
        my $userLogin   = $self->param('login');
        my $oldPassword = $self->param('oldpass');
        my $newPassword = $self->param('newpass');
        $userLogin =~ s/[&,|\\\/= ]//g;

        #$self->app->log->debug($userLogin);

        my $pass = PasswordTools->new();

        my $ldap = Net::LDAP->new( $config->{ldap_host} ) or exit 1;
        my $mesg = $ldap->bind( $config->{ldap_user},
            password => $config->{ldap_password} );
        $mesg = $ldap->search(
            base   => $config->{ldap_search_base},
            filter => "(uid=$userLogin)",
            attrs  => ['userPassword']
        );

        if ( $mesg->count ) {
            my $entry           = $mesg->entry(0);
            my $currentPassword = $entry->get_value('userPassword');
            my $currentDn
                = 'uid=' . $userLogin . ',' . $config->{ldap_search_base};

            #$self->app->log->debug($currentPassword);
            #$self->app->log->debug($currentDn);

            if ($pass->checkPassword(
                    $config->{schema_password}, $currentPassword,
                    $oldPassword
                )
                )
            {
                if ( length($newPassword) > 5 ) {
                    my $tmpCryptPass
                        = $pass->cryptPassword( $config->{schema_password},
                        $newPassword );

                    if ( $config->{enable_samba_password} ) {
                        my $lmHash = lmhash($newPassword);
                        my $ntHash = nthash($newPassword);

                        $mesg = $ldap->modify(
                            $currentDn,
                            replace => {
                                'userPassword'       => $tmpCryptPass,
                                'sambaNTPassword'    => $ntHash,
                                'shadowLastChange'   => int( time() / 86400 ),
                                'sambaPwdLastSet'    => time(),
                                'sambaPwdMustChange' => time()
                                    + 86400 * $config->{password_life},
                                'shadowMax'       => $config->{password_life},
                                'sambaLMPassword' => $lmHash,
                            }
                        );

                    }
                    else {
                        $mesg = $ldap->modify( $currentDn,
                            replace => { 'userPassword' => $tmpCryptPass } );
                        $self->app->log->debug($tmpCryptPass);
                    }

                    $self->render('done');
                }
            }
        }
    }

    $self->render('index');
};

app->mode('production');
app->secret('sdkfjhskjd');
app->start;
__DATA__

@@ index.html.ep
% layout 'default';
% title 'Change password';
<%= form_for index =>(method => 'post') =>  begin %>
Login:<br>
<%= text_field 'login' %><br>
Old password:<br>
<%= password_field 'oldpass' %><br>
New password:<br>
<%= password_field 'newpass' %><br>
<p style="color: #808080; font-size: 10px;">The password will be longer than 5 characters.</p>
<%= submit_button 'Change password' %>
<% end %>

@@ done.html.ep
% layout 'default';
% title 'Success Change password';
<h1>password changed</h1>

@@ layouts/default.html.ep
<!doctype html>
<html>
	<head><title><%= title %></title>
		<style type="text/css">
		.main_block {
		  width : 100%;
		  text-align : center;
		}
		.center_block {
		  width : 200px;
		  margin : 0 auto;
		}
		</style>
  	</head>
	<body>	
		<div class="main_block">
	 		<div class="center_block">
				<%= content %>
			</div>
		</div>
	</body>
</html>

